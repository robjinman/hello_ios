cmake_minimum_required(VERSION 3.15)
project(HelloIOS)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(tinyxml2 REQUIRED)

find_library(UIKIT_LIB UiKit)
find_library(COREMEDIA_LIB CoreMedia)
find_library(METAL_LIB Metal)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")

find_package(Vulkan REQUIRED COMPONENTS MoltenVK)

set(MACOSX_BUNDLE_BUNDLE_NAME "nova")
set(MACOSX_BUNDLE_BUNDLE_VERSION "0.1")
set(MACOSX_BUNDLE_GUI_IDENTIFIER "io.deadbeef.nova")
set(MACOSX_BUNDLE_LONG_VERSION_STRING "0.1")
set(MACOSX_BUNDLE_SHORT_VERSION_STRING "0.1")

add_executable(hello MACOSX_BUNDLE
  src/hello.cpp
  src/hello.mm
  $ENV{VULKAN_SDK}/../iOS/share/vulkan
)

set_source_files_properties(
  $ENV{VULKAN_SDK}/../iOS/share/vulkan
  PROPERTIES
    MACOSX_PACKAGE_LOCATION Resources
)

target_link_libraries(hello
  PRIVATE
    tinyxml2::tinyxml2
    ${UIKIT_LIB}
    ${COREMEDIA_LIB}
    ${METAL_LIB}
    Vulkan::Vulkan
)

set(CMAKE_OSX_DEPLOYMENT_TARGET "17")
set(CMAKE_XCODE_GENERATE_SCHEME "TRUE")
set(CMAKE_XCODE_SCHEME_ENABLE_GPU_API_VALIDATION "FALSE")
set(CMAKE_XCODE_SCHEME_ENABLE_GPU_FRAME_CAPTURE_MODE "DISABLED")

set_target_properties(hello PROPERTIES
  BUNDLE_IDENTIFIER "io.deadbeef.nova"
  XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER "io.deadbeef.nova"
  XCODE_ATTRIBUTE_CODE_SIGNING_ALLOWED "YES"
  XCODE_ATTRIBUTE_CLANG_ENABLE_MODULES "YES"
  XCODE_ATTRIBUTE_LD_RUNPATH_SEARCH_PATHS "@executable_path/Frameworks"
  XCODE_ATTRIBUTE_CODE_SIGN_STYLE "Automatic"
  XCODE_ATTRIBUTE_ONLY_ACTIVE_ARCH "NO"
  MACOSX_BUNDLE_SHORT_VERSION_STRING "0.1"
  MACOSX_BUNDLE_BUNDLE_VERSION "0.1"
  XCODE_ATTRIBUTE_DEVELOPMENT_TEAM "LW57Q4U5U7"
  XCODE_EMBED_FRAMEWORKS "${Vulkan_MoltenVK_LIBRARY};${Vulkan_LIBRARIES};${Vulkan_Layer_VALIDATION}" # TODO: ${Vulkan_Layer_VALIDATION}
  XCODE_EMBED_FRAMEWORKS_CODE_SIGN_ON_COPY "YES"
  XCODE_EMBED_FRAMEWORKS_REMOVE_HEADERS_ON_COPY "YES"
  XCODE_ATTRIBUTE_SKIP_INSTALL "NO"
  XCODE_ATTRIBUTE_INSTALL_PATH "$(LOCAL_APPS_DIR)"
  XCODE_ATTRIBUTE_ASSETCATALOG_COMPILER_APPICON_NAME "AppIcon"
  XCODE_ATTRIBUTE_DEAD_CODE_STRIPPING "NO"
 )
